dnl Process this file with autoconf to produce a configure script.
AC_INIT(ac3/ac3dec_wrap.c)
AM_INIT_AUTOMAKE(ogle, 0.8.0)
dnl AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE
AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC

dnl AM_DISABLE_SHARED
AM_PROG_LIBTOOL

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_BIGENDIAN
AC_C_CONST
AC_C_INLINE


dnl Checks for header files.
AC_CHECK_HEADERS(byteswap.h sys/bswap.h sys/endian.h)

dnl Find X Window System
AC_PATH_XTRA
saved_CPPFLAGS=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $X_CFLAGS"
AC_CHECK_HEADER(X11/extensions/XShm.h, 
		AC_DEFINE([HAVE_XSHM], 1, [X11 supports MIT-XShm]))
CPPFLAGS=$saved_CPPFLAGS
AC_CHECK_LIB(Xext, XShmQueryExtension, 
	     XEXT_LIB=-lXext, 
	     AC_MSG_ERROR(You need libXext), 
	     $X_LIBS -lX11 $X_EXTRA_LIBS)


dnl ---------------------------------------------------------
dnl Xvideo (Xv)
dnl ---------------------------------------------------------

use_xv=yes
AC_ARG_ENABLE(xv, 
  [  --disable-xv            disable the use of Xv ],
  if test x$enableval != xyes; then
    use_xv=no
  fi)
if test x$use_xv = xyes; then
  saved_CPPFLAGS=$CPPFLAGS
  CPPFLAGS="$CPPFLAGS $X_CFLAGS"
  AC_CHECK_HEADER(X11/extensions/Xv.h, xv_header=yes, xv_header=no)
  CPPFLAGS=$saved_CPPFLAGS
  AC_CHECK_LIB(Xv, XvQueryExtension, 
	       [ AC_DEFINE([HAVE_XV], 1, [X11 supports Xv])
		 XEXT_LIB="$XEXT_LIB -lXv"
	        ], 
	       AC_MSG_WARN(You won't bw able to use Xv), 
	       $X_LIBS -lX11 -lXext $X_EXTRA_LIBS)
fi
AC_SUBST(XEXT_LIB)

dnl ---------------------------------------------------------
dnl end Xvideo (Xv)
dnl ---------------------------------------------------------


dnl ---------------------------------------------------------
dnl libjpeg
dnl ---------------------------------------------------------

jpeg_path=""
AC_ARG_WITH(libjpeg, 
  [  --with-libjpeg=path     specify the install prefix to libjpeg ], 
  if test x$withval = xno; then
    AC_MSG_ERROR([You must have/use libjpeg])
  fi
  if test x$withval != xyes; then
    jpeg_path=$withval
  fi)
if test -z "$jpeg_path"; then
  dnl We have nothing better to go on, guess that it's installed in prefix
  if test "x$prefix" != xNONE; then
    jpeg_path=$prefix
  else
    jpeg_path=$ac_default_prefix
  fi  
fi
dnl Both this and the libdvdread should first test and see if any flags
dnl beside -lname are needed. Then they the suggested path (+ some standard?)
AC_CHECK_LIB(jpeg, main, 
  [ JPEG_LIBS="-L$jpeg_path/lib -rpath $jpeg_path/lib -ljpeg"
    JPEG_CFLAGS=-I$jpeg_path/include
  ],
  [ 
    AC_MSG_ERROR([Need libjpeg, install it and/or specify it's location])
  ], -L$jpeg_path/lib)

AC_ARG_WITH(libjpeg-includes, 
  [  --with-libjpeg-includes=path  specify location of libjpeg headers ], 
  JPEG_CFLAGS=-I$with_libjpeg_includes)


saved_CPPFLAGS=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $JPEG_CFLAGS"
AC_CHECK_HEADER(jpeglib.h,, 
  [ AC_MSG_WARN([Can not find jpeglib.h, need the libjpeg headers])
    AC_MSG_ERROR([Install the headers and/or specify their location]) 
  ])
CPPFLAGS=$saved_CPPFLAGS

AC_SUBST(JPEG_CFLAGS)
AC_SUBST(JPEG_LIBS)

dnl ---------------------------------------------------------
dnl end libjpeg
dnl ---------------------------------------------------------


dnl clock_gettime is in librt on Solaris
AC_CHECK_FUNC(clock_gettime, [ AC_DEFINE([HAVE_CLOCK_GETTIME], 1, [ ]) ], 
  AC_CHECK_LIB(rt, clock_gettime, 
    [ AC_DEFINE([HAVE_CLOCK_GETTIME], 1, [ ])
      LIBS="$LIBS -lrt"
    ],
    dnl AC_MSG_WARN(You need clock_gettime)
  )
)

dnl nanosleep is in librt on Solaris
AC_CHECK_FUNC(nanosleep, , 
  AC_CHECK_LIB(rt, nanosleep, , 
    AC_MSG_ERROR(You need nanosleep)
  )
)

dnl Should this also have some fallback library?
AC_CHECK_FUNCS(madvise)


dnl ---------------------------------------------------------
dnl dvdread
dnl ---------------------------------------------------------

dvd_path=""
AC_ARG_WITH(dvdread, 
  [  --with-dvdread=path     specify the install prefix to libdvdread ], 
  if test x$with_dvdread = xno; then
    AC_MSG_ERROR([You must have/use libdvdread])
  fi
  if test x$with_dvdread != xyes; then
    dvd_path=$with_dvdread
  fi)
if test -z "$dvd_path"; then
  dnl We have nothing better to go on, guess that it's installed in prefix
  if test "x$prefix" != xNONE; then
    dvd_path=$prefix
  else
    dvd_path=$ac_default_prefix
  fi  
fi
AC_CHECK_LIB(dvdread, DVDFileSize, 
  [ DVDREAD_LIBS="-L$dvd_path/lib -rpath $dvd_path/lib -ldvdread" 
    DVDREAD_CFLAGS=-I$dvd_path/include
  ],
  [
    AC_CHECK_LIB(dvdread, DVDOpen, 
      [ AC_MSG_ERROR([Upgrade libdvdread to 0.9.0 or later]) ],
      [ AC_MSG_ERROR([Need libdvdread, install it or specify it's location])],
      -L$dvd_path/lib)
  ], -L$dvd_path/lib)

AC_ARG_WITH(dvdread-includes, 
  [  --with-dvdread-includes=path  specify location of libdvdread headers ], 
  DVDREAD_CFLAGS=-I$with_dvdread_includes)

dnl Linux and Solaris needs this to prototype lseek64 and others
dnl Should really be taken from getconf...
DVDREAD_CFLAGS="-D_LARGEFILE64_SOURCE $DVDREAD_CFLAGS"

saved_CPPFLAGS=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $DVDREAD_CFLAGS"
AC_CHECK_HEADER(dvdread/dvd_reader.h,, 
  [ AC_MSG_WARN([Can not find dvd_reader.h, need the libdvdread headers])
    AC_MSG_ERROR([Install the headers and/or specify their location]) 
  ])
CPPFLAGS=$saved_CPPFLAGS

AC_SUBST(DVDREAD_CFLAGS)
AC_SUBST(DVDREAD_LIBS)

dnl ---------------------------------------------------------
dnl end dvdread
dnl ---------------------------------------------------------


dnl ---------------------------------------------------------
dnl Sun MediaLib
dnl ---------------------------------------------------------

have_mlib=no
mlib_path="/opt/SUNWmlib"
AC_ARG_WITH(libmlib,
  [  --with-libmlib=path     specify where mediaLib is installed],
  if test x$with_libmlib != xyes; then
    mlib_path=$with_libmlib
  fi)
if test x$mlib_path != xno; then
  saved_CPPFLAGS=$CFLAGS
  CFLAGS="$CFLAGS -L$mlib_path/lib"
  AC_CHECK_LIB(mlib, mlib_VideoDCT8x8_S16_S16, 
	       [ MLIB_LIBS="-L$mlib_path/lib -rpath $mlib_path/lib -lmlib"
                 MLIB_CFLAGS=-I$mlib_path/include
                 have_mlib=yes
               ],,)
  CFLAGS=$saved_CPPFLAGS
fi
if test x$have_mlib = xyes; then
  saved_CPPFLAGS=$CPPFLAGS
  CPPFLAGS="$CPPFLAGS $MLIB_CFLAGS"
  AC_CHECK_HEADER(mlib_types.h,, 
    [ AC_MSG_WARN([Can not find mlib_types.h, need the mediaLib headers])
      AC_MSG_ERROR([Install the headers and/or specify their location]) 
    ])
  CPPFLAGS=$saved_CPPFLAGS
fi


AC_SUBST(MLIB_CFLAGS)
AC_SUBST(MLIB_LIBS)
AM_CONDITIONAL(MLIB_LIBS, test x$have_mlib = xyes)
if test x$have_mlib = xyes; then
  AC_DEFINE(HAVE_MLIB, 1, [Solaris MultiMedia Library installed])
fi

dnl ---------------------------------------------------------
dnl end Sun MediaLib
dnl ---------------------------------------------------------


dnl ---------------------------------------------------------
dnl liba52
dnl ---------------------------------------------------------

a52_path=""
AC_ARG_WITH(liba52, 
  [  --with-liba52=path      specify the install prefix to liba52 (a52dec) ], 
  if test x$with_liba52 = xno; then
    AC_MSG_ERROR([You must have/use a52dec])
  fi
  if test x$with_liba52 != xyes; then
    a52_path=$with_liba52
  fi)
if test -z "$a52_path"; then
  dnl We have nothing better to go on, guess that it's installed in prefix
  if test "x$prefix" != xNONE; then
    a52_path=$prefix
  else
    a52_path=$ac_default_prefix
  fi  
fi
if test x$have_mlib = xyes; then
  AC_CHECK_LIB(a52, a52_init, 
    [ A52_LIBS="-L$a52_path/lib -rpath $a52_path/lib -la52" 
      A52_CFLAGS=-I$a52_path/include
    ],
    [
      AC_MSG_ERROR([Need liba52, install a52dec or specify it's location])
    ], -L$a52_path/lib -L$mlib_path/lib -lmlib -lm)
else
  AC_CHECK_LIB(a52, a52_init, 
    [ A52_LIBS="-L$a52_path/lib -rpath $a52_path/lib -la52" 
      A52_CFLAGS=-I$a52_path/include
    ],
    [
      AC_MSG_ERROR([Need liba52, install a52dec or specify it's location])
    ], -L$a52_path/lib -lm)
fi

dnl AC_CHECK_LIB(ao, ao_drivers, 
dnl [ A52_LIBS="$A52_LIBS -lao" ],
dnl [ AC_MSG_WARN([Need libao, install a52dec or specify it's location])
dnl   AC_MSG_ERROR([It might also be that you have OggVorbis's libao istalled])
dnl ], -L$a52_path/lib)

AC_ARG_WITH(liba52-includes, 
  [  --with-liba52-includes=path  specify location of liba52 headers ], 
  A52_CFLAGS=-I$with_liba52_includes)

saved_CPPFLAGS=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $A52_CFLAGS"
AC_CHECK_HEADER(a52dec/a52.h,, 
    [ AC_MSG_WARN([Can not find a52dec/a52.h, need the liba52 headers])
      AC_MSG_ERROR([Install a52dec(-dev) and/or specify it's location]) 
    ])
CPPFLAGS=$saved_CPPFLAGS

AC_SUBST(A52_CFLAGS)
AC_SUBST(A52_LIBS)

dnl ---------------------------------------------------------
dnl end liba52
dnl ---------------------------------------------------------

dnl ---------------------------------------------------------
dnl included audio out
dnl ---------------------------------------------------------

dnl check for oss
AC_ARG_ENABLE([oss],
    [  --disable-oss           make a version not using oss])
if test x"$enable_oss" != x"no"; then
    case "$host" in
    *-linux*|*-openbsd*|*-freebsd*)
        AC_DEFINE([LIBAO_OSS],,[libao OSS support])
        AC_CHECK_LIB([ossaudio],[_oss_ioctl],
            [LIBAO_LIBS="$LIBAO_LIBS -lossaudio"]);;
    esac
fi

dnl check for sunaudio
AC_ARG_ENABLE([sunaudio],
    [  --disable-sunaudio make a version not using sunaudio])
if test x"$enable_sunaudio" != x"no"; then
    case "$host" in
    *-sun-solaris*|*-netbsd*)
        AC_DEFINE([LIBAO_SUNAUDIO],,[libao sunaudio support]);;
    esac
fi
AC_SUBST([LIBAO_LIBS])

dnl ---------------------------------------------------------
dnl end included audio out
dnl ---------------------------------------------------------


dnl ---------------------------------------------------------
dnl libxml
dnl ---------------------------------------------------------

AM_PATH_XML2(2.4.5, AC_DEFINE(HAVE_XML, 1, [libxml found]),
  [ AC_MSG_ERROR([Install libxml2(-dev) and/or specify it's location]) ])

dnl ---------------------------------------------------------
dnl end libxml
dnl ---------------------------------------------------------


dnl ---------------------------------------------------------
dnl config file
dnl ---------------------------------------------------------

if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(CONFIG_FILE, "${ac_default_prefix}/share/oglerc")
  CONFIG_FILE="${ac_default_prefix}/share/oglerc"
  CONFIG_FILE_DTD="${ac_default_prefix}/share/ogle_conf.dtd"
else
  AC_DEFINE_UNQUOTED(CONFIG_FILE, "${prefix}/share/oglerc")
  CONFIG_FILE="${prefix}/share/oglerc"
  CONFIG_FILE_DTD="${prefix}/share/ogle_conf.dtd"
fi

AC_SUBST(CONFIG_FILE)
AC_SUBST(CONFIG_FILE_DTD)

case $host_os in
solaris*)
	DEFAULT_DVD_DEVICE="/cdrom/cdrom0"
	;;
linux*)
	DEFAULT_DVD_DEVICE="/dev/dvd"
	;;
freebsd*)
	DEFAULT_DVD_DEVICE="/dev/racd0c"
	;;
netbsd*)
  	case "$host_cpu" in
   	i?86)
		DEFAULT_DVD_DEVICE="/dev/rcd0d"
		;;
   	*)
		DEFAULT_DVD_DEVICE="/dev/rcd0c"
		;;
  	esac
  	;;
openbsd*)
	DEFAULT_DVD_DEVICE="/dev/rcd0c"
	;;
bsdi*)
	DEFAULT_DVD_DEVICE="/dev/sr0c"
	;;
*)
	DEFAULT_DVD_DEVICE=""
	;;
esac

AC_SUBST(DEFAULT_DVD_DEVICE)

dnl ---------------------------------------------------------
dnl end config file
dnl ---------------------------------------------------------




dnl ---------------------------------------------------------
dnl Compiler specific stuff
dnl ---------------------------------------------------------

if test x"$GCC" = x"yes"; then
  CFLAGS="$CFLAGS -Wall"
else
    dnl non-GCC flags - we probably need exact configuration triplets here.
    case "$host_alias" in
    sparc-sun-solaris*)
        CFLAGS="$CFLAGS -xCC -fast"
	DVDREAD_CFLAGS="$DVDREAD_CFLAGS -xmemalign"
    esac
fi

dnl ---------------------------------------------------------
dnl end Compiler specific stuff
dnl ---------------------------------------------------------


dnl ---------------------------------------------------------
dnl Architecture specific stuff
dnl ---------------------------------------------------------

USE_MMX=
USE_ALTIVEC=
USE_SPARCASM=
case "$host_cpu" in
i?86)
	if test x"$GCC" = x"yes"; then
	  CFLAGS="$CFLAGS -fomit-frame-pointer -funroll-loops"
dnl Do we need this ? should we hard code it to pentium/pentiumpro?
dnl what about k6 / athlon
dnl will it still assemble the mmx instructions when -march=i386
dnl	  CFLAGS="$CFLAGS -march=$host_cpu"
	  CFLAGS="$CFLAGS -march=i586 -mcpu=pentiumpro"
	fi
	USE_MMX=yes
	;;
sparc)
	if test x"$GCC" = x"yes"; then
	  CFLAGS="$CFLAGS -mcpu=ultrasparc -mvis"
	  AC_DEFINE([USE_SPARCASM],1,[use assembler routines])
	  USE_SPARCASM=yes
	fi
	;;
powerpc|ppc)
	if test x"$GCC" = x"yes"; then
	  CFLAGS="$CFLAGS -fomit-frame-pointer -funroll-loops"
dnl is the right? needed? use just -m7400 ? need new gcc for that..
dnl is there a -mcpu -march flag that we should use?
dnl should be a try compile on this line.. 
	  CFLAGS="$CFLAGS -Wa,-m7400"
dnl that sets USE_ALTIVEC depending on the success
	  USE_ALTIVEC=yes
	fi
esac

AC_ARG_ENABLE(altivec, 
  [  --disable-altivec       disable the use of Altivec (G4+)],
  if test x$enableval != xyes; then
    USE_ALTIVEC=
  fi)
if test x$USE_ALTIVEC = xyes; then
  AC_DEFINE([HAVE_ALTIVEC],1,[Processor support ALTIVEC])
fi

AC_ARG_ENABLE(mmx, 
  [  --disable-mmx           disable the use of MMX (x86)],
  if test x$enableval != xyes; then
    USE_MMX=
  fi)
if test x$USE_MMX = xyes; then
  AC_DEFINE([HAVE_MMX],1,[Processor support MMX])
fi

AC_SUBST(USE_MMX)
AM_CONDITIONAL(USE_MMX, test x$USE_MMX = xyes)
AC_SUBST(USE_ALTIVEC)
AM_CONDITIONAL(USE_ALTIVEC, test x$USE_ALTIVEC = xyes)
AC_SUBST(USE_SPARCASM)
AM_CONDITIONAL(USE_SPARCASM, test x$USE_SPARCASM = xyes)

dnl ---------------------------------------------------------
dnl end Architecture specific stuff
dnl ---------------------------------------------------------

TOP_INCLUDES='-I. -I$(top_srcdir) -I$(top_srcdir)/include'
AC_SUBST(TOP_INCLUDES)


AC_OUTPUT(Makefile 
ogle/Makefile 
include/Makefile 
common/Makefile 
ctrl/Makefile 
ac3/Makefile 
libao/Makefile 
mpeg2_video/Makefile 
vmg/Makefile 
mpeg2_program/Makefile 
scripts/Makefile
scripts/ogle
doc/Makefile
doc/man/Makefile
doc/man/oglerc.5
doc/man/ogle.1
dvd_cli/Makefile
oglerc)
