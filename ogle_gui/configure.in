dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(ogle_gui, 0.8.2-cvs)
AM_CONFIG_HEADER(config.h)

dnl Check that ogle is installed in the prefix, this is needed
if test "x${exec_prefix}" = "xNONE"; then
  if test "x${prefix}" = "xNONE"; then
    ogle_pkglibdir=${ac_default_prefix}/lib/ogle
  else
    ogle_pkglibdir=${prefix}/lib/ogle
  fi
else
  ogle_pkglibdir=${exec_prefix}/lib/ogle
fi

AC_MSG_CHECKING(for ogle helper dir)
if test ! -x ${ogle_pkglibdir}/ogle_ctrl; then
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([* You must install ogle and ogle_gui in the same prefix])
fi
AC_MSG_RESULT(yes)

AC_PROG_CC
AC_ISC_POSIX
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/param.h)

AM_PROG_LIBTOOL

dnl Add the languages which your application supports here.
ALL_LINGUAS="de fr nl sv"
AM_GNU_GETTEXT

AM_PATH_GTK(,,[AC_MSG_ERROR([You must have gtk+ development files])])

AM_PATH_LIBGLADE(,[AC_MSG_ERROR([You must have libglade development files])])

dnl Hack to get the path to the libglade so-file
LIBGLADE_PREFIX=`$LIBGLADE_CONFIG --prefix`
AC_DEFINE_UNQUOTED(LIBGLADE_LIBDIR, "${LIBGLADE_PREFIX}/lib",
  [Prefix for libglade])
dnl this should be something different for some bsd (libglade.so.4 i
dnl think), but I don't know how to check for it properly
AC_DEFINE_UNQUOTED(LIBGLADE_LIB, "libglade.so.0",
  [Name of the libglade lib (for dlopen)])

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, 
    "${ac_default_prefix}/${DATADIRNAME}/locale",
    [Dir for the locale files])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl PACKAGE_PIXMAPS_DIR is where the pixmaps (and the glade file) for
dnl the gui will reside
if test "x${prefix}" = "xNONE"; then
   PACKAGE_PIXMAPS_DIR="${ac_default_prefix}/share/ogle_gui/"
else
   PACKAGE_PIXMAPS_DIR="${prefix}/share/ogle_gui/"
fi
AC_SUBST(PACKAGE_PIXMAPS_DIR)
AC_DEFINE_UNQUOTED(PACKAGE_PIXMAPS_DIR, "$PACKAGE_PIXMAPS_DIR", [Get pixmaps])

AM_PATH_XML2(2.4.19, AC_DEFINE(HAVE_XML, 1, [libxml found]),
  [ AC_MSG_ERROR([Install libxml2(-dev) and/or specify it's location]) ])

dvd_path=""
AC_ARG_WITH(dvdcontrol, 
  [  --with-dvdcontrol=DIR   Specify the install prefix to libdvdcontrol ], 
  if test x$withval = xno; then
    [AC_MSG_ERROR([* You must use libdvdcontrol, it's a part of Ogle])]
  fi
  if test x$withval != xyes; then
    dvd_path=$withval
  fi)
if test -z "$dvd_path"; then
  dnl We have nothing better to go on, guess that it's installed in prefix
  if test "x$prefix" != xNONE; then
    dvd_path=$prefix
  else
    dvd_path=$ac_default_prefix
  fi  
fi

AC_CHECK_LIB(dvdcontrol, DVDRequestInput, 
  [ DVDLIBS="-L$dvd_path/lib/ogle -rpath $dvd_path/lib/ogle -ldvdcontrol" 
    DVD_INCLUDEDIR=-I$dvd_path/include
  ],
  [ AC_MSG_ERROR([
* Need "libdvdcontrol.so", it's part of Ogle(-devel).
* Install it and/or specify the location for it using --with-dvdcontrol=DIR.
* For example ... --with-dvdcontrol=/usr/local ] )
  ],
  -L$dvd_path/lib/ogle -lmsgevents)
AC_SUBST(DVDLIBS)

AC_ARG_WITH(dvd-includes, 
  [  --with-dvd-includes=DIR Specify location of DVD headers ], 
  DVD_INCLUDEDIR=-I$with_dvd_includes)

dnl if test x"$DVD_INCLUDEDIR" = x"-I/usr/local/include"; then
dnl  DVD_INCLUDEDIR=""
dnl fi
if test x"$DVD_INCLUDEDIR" = x"-I/usr/include"; then
  DVD_INCLUDEDIR=""
fi

saved_CPPFLAGS=$CPPFLAGS
dnl -w added to shutup GCC3.1's cpp warning about -I/usr/local
CPPFLAGS="-w $CPPFLAGS $DVD_INCLUDEDIR"
AC_CHECK_HEADER(ogle/dvd.h, ,
  [ AC_MSG_ERROR([
* Need "ogle/dvd.h", it's part of Ogle(-devel).
* Install it and/or specify the location for it using --with-dvd-includes=DIR. 
* For example ... --with-dvd-includes=/usr/local/include ] )
  ]
)
CPPFLAGS=$saved_CPPFLAGS
AC_SUBST(DVD_INCLUDEDIR)

dnl check for pthreads
AC_CHECK_LIB(pthread, pthread_create, PTHREAD_LIB="-lpthread",
	[AC_CHECK_LIB(pthreads, pthread_create, PTHREAD_LIB="-lpthreads",
	    [AC_CHECK_LIB(c_r, pthread_create, PTHREAD_LIB="-lc_r",
		[AC_CHECK_FUNC(pthread_create)]
	    )]
	)]
)
AC_SUBST(PTHREAD_LIB)

dnl Architecture specific stuff
AC_CANONICAL_HOST
case "$host_os" in
	solaris*)
		AC_DEFINE(SOLARIS, 1, [This is a Solaris system])
		;;
	linux*)
		AC_DEFINE(LINUX, 1, [This is a Linux system])
		;;
	*)
		;;
esac



dnl ---------------------------------------------------------
dnl config file
dnl ---------------------------------------------------------

if test "x${prefix}" = "xNONE"; then
  CONFIG_FILE="${ac_default_prefix}/share/ogle/oglerc"
  CONFIG_FILE_DTD="${ac_default_prefix}/share/ogle/ogle_conf.dtd"
else
  CONFIG_FILE="${prefix}/share/ogle/oglerc"
  CONFIG_FILE_DTD="${prefix}/share/ogle/ogle_conf.dtd"
fi
AC_DEFINE_UNQUOTED(CONFIG_FILE, "${CONFIG_FILE}",
  [Location of the oglerc config file])

AC_SUBST(CONFIG_FILE)
AC_SUBST(CONFIG_FILE_DTD)

dnl ---------------------------------------------------------
dnl end config file
dnl ---------------------------------------------------------

dnl enable warnings
CFLAGS="$CFLAGS -Wall"

AC_OUTPUT([
intl/Makefile
po/Makefile.in
Makefile
pixmaps/Makefile
src/Makefile
])


