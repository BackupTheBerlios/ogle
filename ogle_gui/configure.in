dnl Process this file with autoconf to produce a configure script.

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(ogle_gui, 0.7.5)
AM_CONFIG_HEADER(config.h)


dnl Check that ogle is installed in the prefix, this is needed
if test "x${exec_prefix}" = "xNONE"; then
  if test "x${prefix}@" = "xNONE"; then
    ogle_pkglibdir=${ac_default_prefix}/lib/ogle
  else
    ogle_pkglibdir=${prefix}/lib/ogle
  fi
else
  ogle_pkglibdir=${exec_prefix}/lib/ogle
fi

AC_MSG_CHECKING(for ogle helper dir)
if test ! -x ${ogle_pkglibdir}/ogle_ctrl; then
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([* You must install ogle and ogle_gui in the same prefix])
fi
AC_MSG_RESULT(yes)

dnl Pick up the Gnome macros.
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS

dnl Add the languages which your application supports here.
ALL_LINGUAS=""
AM_GNU_GETTEXT

dnl Set PACKAGE_LOCALE_DIR in config.h.
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Subst PACKAGE_PIXMAPS_DIR.
dnl PACKAGE_PIXMAPS_DIR="`gnome-config --datadir`/pixmaps/${PACKAGE}"
dnl AC_SUBST(PACKAGE_PIXMAPS_DIR)

if test "x${prefix}" = "xNONE"; then
   PACKAGE_PIXMAPS_DIR="${ac_default_prefix}/share/pixmaps/"
else
   PACKAGE_PIXMAPS_DIR="${prefix}/share/pixmaps/"
fi
AC_SUBST(PACKAGE_PIXMAPS_DIR)
AC_DEFINE_UNQUOTED(PACKAGE_PIXMAPS_DIR, "$PACKAGE_PIXMAPS_DIR", [Get pixmaps])


dvd_path=""
AC_ARG_WITH(dvdcontrol, 
  [  --with-dvdcontrol=DIR   Specify the install prefix to libdvdcontrol ], 
  if test x$withval = xno; then
    AC_MSG_ERROR([* You must use libdvdcontrol, it's a part of Ogle])
  fi
  if test x$withval != xyes; then
    dvd_path=$withval
  fi)
if test -z "$dvd_path"; then
  dnl We have nothing better to go on, guess that it's installed in prefix
  if test "x$prefix" != xNONE; then
    dvd_path=$prefix
  else
    dvd_path=$ac_default_prefix
  fi  
fi

AC_CHECK_LIB(dvdcontrol, DVDRequestInput, 
  [ DVDLIBS="-L$dvd_path/lib/ogle -rpath $dvd_path/lib/ogle -ldvdcontrol" 
    DVD_INCLUDEDIR=-I$dvd_path/include
  ],
  [ AC_MSG_ERROR([
* Need "libdvdcontrol.so", it's part of Ogle(-devel).
* Install it and/or specify the location for it using --with-dvdcontrol=DIR.
* For example ... --with-dvdcontrol=/usr/local ] )
  ],
  -L$dvd_path/lib/ogle -lmsgevents)

AC_ARG_WITH(dvd-includes, 
  [  --with-dvd-includes=DIR Specify location of DVD headers ], 
  DVD_INCLUDEDIR=-I$with_dvd_includes)
saved_CPPFLAGS=$CPPFLAGS
CPPFLAGS="$CPPFLAGS $DVD_INCLUDEDIR"
AC_CHECK_HEADER(ogle/dvd.h, ,
  [ AC_MSG_ERROR([
* Need "ogle/dvd.h", it's part of Ogle(-devel).
* Install it and/or specify the location for it using --with-dvd-includes=DIR. 
* For example ... --with-dvd-includes=/usr/local/include ] )
  ]
)
CPPFLAGS=$saved_CPPFLAGS

AC_SUBST(DVDLIBS)
AC_SUBST(DVD_INCLUDEDIR)


dnl Architecture specific stuff
AC_CANONICAL_HOST
case "$host_os" in
	solaris*)
		AC_DEFINE(SOLARIS, 1, [This is a Solaris system])
		;;
	linux*)
		AC_DEFINE(LINUX, 1, [This is a Linux system])
		;;
	*)
		;;
esac

AC_OUTPUT([
Makefile
macros/Makefile
src/Makefile
intl/Makefile
po/Makefile.in
doc/Makefile
doc/C/Makefile
])


